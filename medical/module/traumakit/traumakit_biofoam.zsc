extend class UaS_TraumaKit {
	void HandleBioFoam() {
		statusMessage = statusmessage..Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_TITLE");
		statusMessage = statusmessage..Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_DESCRIPTION");
		statusMessage = statusmessage..Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_REMAINING")..weaponstatus[TKS_BIOFOAM]..Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_NEWLINES");
		if (!mti.currentWound) { return; }
		if (mti.currentWound.open <= 0) { return; }
		if (weaponstatus[TKS_BIOFOAM] <=0) {
			currentmessage.text = Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_NONELEFT");
			currentmessage.timeout = 2*35;
			return;
		}

		// Status precheck
		if (mti.currentWound.cavity < 0) {
			currentmessage.text = Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_UNNECESSARY");
			currentmessage.timeout = 10;
			return;
		}

		// Handle actions
		if (weaponstatus[TK_HOLD] < 0) {
			weaponstatus[TK_HOLD] = min(weaponstatus[TK_HOLD] + 1, 0);
			return;
		}
		if ((owner.player.cmd.buttons & BT_ATTACK)) {
			if (!HandleStrip()) { return; }
			if (!(owner.player.oldbuttons & BT_ATTACK)) {
				weaponstatus[TK_HOLD] = random[uas_tk](20,35);
				owner.A_StartSound("UaS/traumakit/biofoam", CHAN_WEAPON, CHANF_DEFAULT, 0.1);
				mti.patient.A_MuzzleClimb(
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1))
				);
			}
			else {
				weaponstatus[TK_HOLD]--;
			}

			if (weaponstatus[TK_HOLD] > 0) {
				currentmessage.text = Stringtable.Localize("$UAS_TRAUMAKIT_BIOFOAM_INJECTING");
				currentmessage.timeout = 10;

				weaponstatus[TKS_BIOFOAM]--;

				if (mti.currentWound.inpain(8)) {
					weaponstatus[TK_HOLD] = -5;
					mti.currentWound.inpain(35);
					PainJolt();
				}

				// semi-normalized distribution for success check
				int check = (
					frandom[uas_tk](0,5) +
					frandom[uas_tk](0,10)) / 2;

				// remove some tissue damage
				if (check < mti.currentWound.cavity) {
					mti.patient.A_StartSound("misc/bulletflesh",CHAN_BODY, CHANF_DEFAULT, 0.2);
					mti.currentWound.cavity = max(mti.currentWound.cavity - frandom(0.1, 0.4), 0);
					//if(random[uas_tk](0,5)) { PlaySkinSound(SKINSOUND_GRUNT,"*grunt"); }
					mti.patient.A_MuzzleClimb(
						(frandom[uas_tk](-.1,.1),frandom[uas_tk](-.1,.1)),
						(frandom[uas_tk](-.1,.1),frandom[uas_tk](-.1,.1)),
						(frandom[uas_tk](-.1,.1),frandom[uas_tk](-.1,.1)),
						(frandom[uas_tk](-.1,.1),frandom[uas_tk](-.1,.1)));
				}

				// maybe remove some infection. Biofoam better at this than saline
				if (check < mti.currentWound.infection * 2 && random[uas_tk](0,1) == 0) {
					mti.currentWound.infection = max(mti.currentWound.infection - 1, 1);
				}
			}
		}
	}
}
