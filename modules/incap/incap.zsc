class UaS_Incap_Handler : Inventory {
	int bleedout;
	bool incap;

	default {
		+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.UNDROPPABLE;
		-INVENTORY.INVBAR;
	}

	override void PostBeginPlay() {
		bleedout = 4200;
	}

	override void DoEffect() {
		super.DoEffect();
		let owner = HDPlayerPawn(owner);

		switch(incap) {

			// Normal
			case false:
				bleedout = min(bleedout + 1, 4200);

				// Activate incap shield
				if(bleedout == 4200 && owner.bBUDDHA == false) {
					owner.bBUDDHA = true;
					if(UaS_Debug & UDF_INCAP) { owner.A_Log("Incap shield active"); }
				}

				// Enter incap state
				if(owner.health == 1 && owner.stimcount < 20) {
					incap = true;
					if(UaS_Debug & UDF_INCAP) { owner.A_Log("Incapacitated"); }
				}
				break;

			// Incapacitation
			case true:
				bleedout = max(bleedout - 1, 0);

				// Clamp important attributes
				owner.health = clamp(owner.health, 1, 1);
				owner.player.CrouchFactor = clamp(owner.player.CrouchFactor, 0.5, 0.5);
				owner.A_SelectWeapon("Ring");

				// Kill player if they bleed out
				if(bleedout == 0 ) {
					incap = false;
					owner.bBUDDHA = false;
					owner.A_Die('bleedout');
				}

				// Exit incap state
				if(owner.stimcount >= 20) {
					incap = false;
					owner.health += 1;
					owner.bBUDDHA = false;
					if(UaS_Debug & UDF_INCAP) { owner.A_Log("Revived"); }
				}
				break;
		}
	}
}
