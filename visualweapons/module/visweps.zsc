class UaS_VisWeps_Bootstrap : EventHandler {
	override void WorldThingSpawned(WorldEvent e) {
		if(!e.Thing) { return; }
		if(!UaS.CVarCheck("UaS_VisWepsEnabled") || UaS_VisWepsEnabled) {
			if(e.Thing is 'HDMarine') {
				e.Thing.GiveInventory('UaS_VisWeps_Updater', 1);
			}
		}
	}
}

class UaS_VisWeps_Updater : Inventory {
	bool usingPistol;

	default {
		Inventory.MaxAmount 1;
		+INVENTORY.PERSISTENTPOWER;
	}

	override void DoEffect() {
		super.DoEffect();
		let ownr = HDMarine(Owner);
		if(ownr is 'HDMarine' && ownr.health > 0) {

			// Check if the marine is using their pistol
			if(ownr.CurState == ownr.ResolveState("shootpistol")) {
				usingPistol = true;
			}
			else if(ownr.CurState == ownr.ResolveState("reload")) {
				usingPistol = false;
			}

			// Override sprite accordingly
			if(!usingPistol) {
				switch(ownr.wep) {
					case 2: // HDMW_HUNTER
						ownr.sprite = GetSpriteIndex('PLA3');
						break;
					case 3: // HDMW_SMG
						ownr.sprite = GetSpriteIndex('PSMG');
						break;
					case 4: // HDMW_ROCKET
						ownr.sprite = GetSpriteIndex('PLA5');
						break;
				}
			}
			else {
				ownr.sprite = GetSpriteIndex('PLA2');
			}
		}
	}

	states {
		Precache:
			PLA3 A 1;
			PSMG A 1;
			PLA2 A 1;
			PLA5 A 1;
			stop;
	}
}
