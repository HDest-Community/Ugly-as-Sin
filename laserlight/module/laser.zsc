const UAS_HDLD_LASERLIGHT = "llm";

class UaS_LaserLightModule : HDWeapon {
	int modeLight, modeLaser;
	bool activated;
	UaS_LLM_Emitter emitter;
	vector3 oldsc;
	double offset;
	double angleoffset,pitchoffset;
	color LaserColor;
	double LaserAlpha;
	transient cvar beams;
	transient cvar laserpref, lightpref;

	default {
		radius 3;
		height 3;
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "Picked up a combo laser/light module.";
		Inventory.Icon "UGSIe0";
		HDWeapon.refid UAS_HDLD_LASERLIGHT;
		tag "Light/Laser module";
		scale 0.5;
		+INVENTORY.INVBAR;
		+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.IGNORESKILL;
		+WEAPON.NO_AUTO_SWITCH;
		+hdweapon.fitsinbackpack;
	}

	override double weaponbulk() { return 5; }

	states {
		spawn:
			UGSI E -1;
			stop;
	}

	void ToggleSwitch() {
		ToggleSound();
		activated = !activated;
	}

	void ToggleSound() {
		owner.A_StartSound("UaS/llm/toggle",
			CHAN_AUTO,
			CHANF_OVERLAP,
			volume: frandom[llm](0.8,1.0),
			attenuation: ATTN_STATIC,
			pitch: frandom[llm](0.9, 1.1));
	}

	override void PostBeginPlay() {
		super.PostBeginPlay();
		offset = 12;
		if (!laserpref) { laserpref = CVar.GetCVar("UaS_LLM_LaserMode", owner.player); }
		if (!lightpref) { lightpref = CVar.GetCVar("UaS_LLM_LightMode", owner.player); }
		modeLaser = laserpref.GetInt();
		modeLight = lightpref.GetInt();
	}

	override bool Use(bool pickup) {
		if (owner.player.cmd.buttons & BT_USE) {
			return super.Use(pickup);
		}
		ToggleSwitch();
		return false;
	}

	override void DoEffect() {
		if (!emitter) { emitter = UaS_LLM_Emitter(actor.spawn("UaS_LLM_Emitter", owner.pos, false)); }
		if (owner is 'PlayerPawn') { Configuring(); }
		UpdateEmitter();
		ProcessIntents();
		if(activated && owner.health > 0) {
			Light(modeLight);
			Laser(modeLaser);
		}
		else {
			Light(0);
			Laser(0);
		}
		super.doEffect();
	}

	void Configuring() {
		if (!(owner.player.readyweapon is 'UaS_LaserLightModule')) { return; }
		bool pressingAttack = ((owner.player.cmd.buttons & BT_ATTACK) && !(owner.player.oldbuttons & BT_ATTACK));
		bool pressingAltAttack = ((owner.player.cmd.buttons & BT_ALTATTACK) && !(owner.player.oldbuttons & BT_ALTATTACK));
		if (pressingAttack) { modeLight = (modeLight + 1) % 3; ToggleSound(); }
		if (pressingAltAttack) { modeLaser = (modeLaser + 1) % 3; ToggleSound(); }
		string modeLightName, modeLaserName;
		switch (modeLaser) {
			case 0: modeLaserName = "Off"; break;
			case 1: modeLaserName = "Visible"; break;
			case 2: modeLaserName = "IR only"; break;
		}
		switch (modeLight) {
			case 0: modeLightName = "Off"; break;
			case 1: modeLightName = "Visible"; break;
			case 2: modeLightName = "IR only"; break;
			case 3: modeLightName = "Flashing"; break;
		}
		string statusMessage = "--- Laser / Flashlight ---\n"
			.."Light Mode: "..modeLightName.."\n"
			.."Laser Mode: "..modeLaserName.."\n\n";
		if (activated) { statusMessage=statusMessage.."\cj=On="; }
		else { statusMessage=statusMessage.."\cu=Off="; }
		A_WeaponMessage(statusMessage);
	}

	void ProcessIntents() {
		UaS_Intent oi;
		oi = UaS.GetIntent("LLM_Toggle", owner);
		if (oi) {
			if (oi.Payload ~== "On") { activated = true; }
			else if (oi.Payload ~== "Off") { activated = false; }
			else { ToggleSwitch(); }
			oi.Clear();
		}

		oi = UaS.GetIntent("LLM_ModeSwitch", owner);
		if (oi) {
			int m = oi.Payload.ToInt(10);
			switch(m) {
				case 0: modeLaser = 0; modeLight = 1; break;
				case 1: modeLaser = 1; modeLight = 0; break;
				case 2: modeLaser = 1; modeLight = 1; break;
				case 3: modeLaser = 0; modeLight = 3; break;
			}
			oi.Clear();
		}
	}

	void UpdateEmitter() {
		vector3 newpos;
		double newangle, newpitch;
		if (owner is "HDPlayerPawn") {
			HDPlayerPawn p = HDPlayerPawn(owner);
			actor sc = p.scopecamera;
			if (!p || !sc) { return; }
			newpos = owner.vec3Angle(
				offset * cos(-sc.pitch) + 4 * sin(-sc.pitch),
				sc.angle,
				offset * sin(-sc.pitch) + ((owner.player.viewz - owner.pos.z) - 4));
			newpos.z = min(
				newpos.z,
				owner.pos.z + HDWeapon(HDPlayerPawn(owner).player.readyweapon).GetShootOffset(owner));
			newangle = sc.angle;
			newpitch = sc.pitch;
		}
		else {
			bool BotClass = (
				owner is "HERPBot" ||
				owner is "DERPBot"
			);
			bool HumanClass = (
				owner is "ZombieStormtrooper" ||
				owner is "HideousShotgunGuy" ||
				owner is "VulcanetteGuy" ||
				owner is "HDMarine"
			);
			if (HumanClass) { offset = 8; }
			else if (BotClass) {
				if (owner is "HERPBot") {
					modeLaser = 1;
					modeLight = 0;
					activated = true;
					offset = 8;
				}
				if (owner is "DERPBot") {
					modeLaser = 0;
					modeLight = 1;
					activated = true;
					offset = 2;
				}
			}

			bool isowneraiming = (
				owner.frame ==  4 || owner.frame ==  5 ||
				owner.frame == 23 || owner.frame == 24
			);

			// Handle random light movement
			if ((level.maptime % 10 == 0) && !BotClass) {
				if (owner.target) {
					angleoffset = frandom(-35, 35);
					pitchoffset = frandom(-25, 25);
					//if (!isowneraiming) { llm.pitchoffset += 10; }
				}
				else if (random(0, 1) == 0) {
					angleoffset = frandom(-20, 20);
					pitchoffset = frandom(-10, 10);
					//if (!isowneraiming) { llm.pitchoffset += 20; }
				}
			}
			// if (isowneraiming && mode == 3) {
				// angleoffset = 0;
				// pitchoffset = 0;
			// }

			newpos = owner.vec3Angle(
				offset * cos(-owner.pitch),
				owner.angle,
				offset * sin(-owner.pitch) + (owner.height * 0.7));
			newangle = emitter.angle + (0.15 * deltaangle(emitter.angle, owner.angle + angleoffset));
			newpitch = emitter.pitch + (0.15 * deltaangle(emitter.pitch, owner.pitch + pitchoffset));
		}
		emitter.setOrigin(newpos, true);
		emitter.A_SetAngle(newangle, true);
		emitter.A_SetPitch(newpitch, true);
		emitter.vel = owner.vel;
		emitter.source = self;
	}

	void Light(int mode) {
		switch (mode) {
			case 0: emitter.Deactivate(self); return; break;
			case 1: emitter.IRMode(false); break;
			case 2: emitter.IRMode(true); break;
			case 3:
				if (level.maptime % 4 == 0) { emitter.Deactivate(self); return; }
				break;
		}

		bool ShowIR = false;
		if (players[consoleplayer].mo.checklocalview()) {
			PlayerPawn plr = players[consoleplayer].mo;
			PortableLiteAmp nvg = PortableLiteAmp(plr.FindInventory("PortableLiteAmp"));
			if (nvg && nvg.worn) { ShowIR = true; }
		}
		if (!ShowIR && mode == 2) { emitter.Deactivate(self); return; }

		emitter.Activate(self); // turn on dynlight

		// Draw flashlight particle beam
		if (!beams) { beams = CVar.GetCVar("UaS_FlashlightParticles", players[consoleplayer]); }
		if (beams.GetBool() == false || (ShowIR && owner == players[consoleplayer].mo)) { return; }
		FLineTraceData flTrace;
		emitter.LineTrace(emitter.angle, 64, emitter.pitch, TRF_THRUACTORS, data: flTrace);
		int start = 1; //particle beam starts at emitter
		if(owner.CheckLocalView()) { start = 12; } // unless local player
		if(owner is "DERPBot") { start = 4; } // Reduce beams on DERPs
		for (double d = start; d < flTrace.distance; d = d + (d / 2)) {
			emitter.A_SpawnParticle("White", SPF_FULLBRIGHT|SPF_RELANG,
				1, (sin(20) * d * 8), 0,
				cos(emitter.pitch) * cos(emitter.angle) * (d),
				cos(emitter.pitch) * sin(emitter.angle) * (d),
				sin(-emitter.pitch) * (d),
				owner.vel.x, owner.vel.y, owner.vel.z,
				0, 0, 0,
				(0.75) / (d ** 1.1), 0, 0);
		}
	}

	void Laser(int mode) {
		switch (mode) {
			case 0: return; break;
			case 1: LaserAlpha = 1.0; break;
			case 2: LaserAlpha = 0.0; break;
		}

		// Enemies use red lasers
		if (owner.bISMONSTER && !owner.bFRIENDLY) { LaserColor = "RED"; }
		else { LaserColor = "GREEN"; }

		// Make laser particles larger for players wearing NVGs, and IR lasers visible
		double sizefactor = 1;
		if (players[consoleplayer].mo.checklocalview()) {
			PlayerPawn plr = players[consoleplayer].mo;
			PortableLiteAmp nvg = PortableLiteAmp(plr.FindInventory("PortableLiteAmp"));
			if (nvg && nvg.worn) {
				sizefactor *= clamp(nvg.amplitude * 0.1, 1, 3);
				if (nvg.amplitude > 0) { LaserAlpha = 1.0; }
			}
		}

		// Draw laser particle beam
		FLineTraceData laserTrace;
		emitter.LineTrace(emitter.angle, 16384, emitter.pitch, TRF_THRUACTORS, data: laserTrace);
		for (double f = 0; f < laserTrace.distance; f = f + random[llm](32, 256)) {
			double bliplength = random[llm](8, 32);
			for (double d = f; d < f + bliplength; d = d + frandom[llm](0.1, 1)) {
				if (d > laserTrace.distance) { break; }
				double ps = frandom[llm](min(-f, 0), min(256,laserTrace.distance - d));
				emitter.A_SpawnParticle(LaserColor, SPF_FULLBRIGHT|SPF_RELANG,
					1,
					(frandom[llm](0.75, 1.0) * clamp(d/4096, 0.75, 1)) * sizefactor,
					0,
					cos(emitter.pitch) * cos(emitter.angle) * (d),
					cos(emitter.pitch) * sin(emitter.angle) * (d),
					sin(-emitter.pitch) * (d),
					owner.vel.x + (ps * cos(emitter.angle) * cos(-emitter.pitch)),
					owner.vel.y + (ps * sin(emitter.angle) * cos(-emitter.pitch)),
					owner.vel.z + (ps * sin(-emitter.pitch)),
					0, 0, 0,
					(frandom[llm](0.5, 0.9) * clamp(d/32, 0.0, 1)) * LaserAlpha,
					-1, 0);
			}
			f += bliplength;
		}
	}

	//override int getsbarnum(int flags){ return mode; }

	override inventory CreateTossable(int amt) {
		if(emitter) { emitter.destroy(); }
		return super.CreateTossable(amt);
	}

	/*
	override void actualpickup(actor other) {
		super.actualpickup(other);
		UaS_Respirator pu = UaS_Respirator(other.findinventory("UaS_Respirator", false));
		pu.capacity = capacity;
	}*/
}

class UaS_LLM_Emitter : DynamicLight {
	actor source;
	default {
		DynamicLight.Type "Point";
		+DYNAMICLIGHT.ATTENUATE;
		+DYNAMICLIGHT.SPOT
	}

	override void PostBeginPlay() {
		super.PostBeginPlay();
		Deactivate(self);
		args[DynamicLight.LIGHT_RED] = 255;
		args[DynamicLight.LIGHT_GREEN] = 255;
		args[DynamicLight.LIGHT_BLUE] = 255;
		args[DynamicLight.LIGHT_INTENSITY] = 255;
		SpotInnerAngle = 0;
		SpotOuterAngle = 25;
	}

	void IRMode(bool on) {
		if (on) {
			args[DynamicLight.LIGHT_RED] = 64;
			args[DynamicLight.LIGHT_GREEN] = 63;
			args[DynamicLight.LIGHT_BLUE] = 62;
		}
		else {
			args[DynamicLight.LIGHT_RED] = 255;
			args[DynamicLight.LIGHT_GREEN] = 255;
			args[DynamicLight.LIGHT_BLUE] = 255;
		}
	}

	override void Tick() {
		super.tick();
		if (!source) Destroy();
	}
}
