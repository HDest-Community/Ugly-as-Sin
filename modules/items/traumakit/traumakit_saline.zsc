extend class UaS_TraumaKit {
	void HandleSaline() {
		statusMessage = statusmessage.."\cjSaline\n";
		statusMessage = statusmessage.."\cuNeutral wound debriding solution.\n\n";
		if (!currentWound) { return; }

		// Handle actions
		if ((owner.player.cmd.buttons & BT_ATTACK)) {
			if (!(owner.player.oldbuttons & BT_ATTACK)) {
				weaponstatus[TK_HOLD] = random[uas_tk](20,35);
				owner.A_StartSound("misc/smallslop", CHAN_WEAPON);
			}
			else {
				weaponstatus[TK_HOLD]--;
			}

			if (weaponstatus[TK_HOLD] > 0) {
				currentmessage.text = "Flushing the wound...\n";
				currentmessage.timeout = 10;

				// semi-normalized distribution for success check
				int check = (
					random[uas_tk](0,100) +
					random[uas_tk](0,100)) / 2;

				if (!currentWound.numb()) {
					weaponstatus[TK_HOLD] = -1;
					PainJolt();
				}

				// remove some dirt
				if (check < currentWound.dirty) {
					owner.A_StartSound("misc/smallslop", CHAN_WEAPON);
					currentWound.dirty = max(currentWound.dirty - 1, 0);
					if(random[uas_tk](0, 10) == 0) { owner.A_StartSound("*grunt",CHAN_VOICE); }
				}

				// maybe remove some infection
				if (check < currentWound.infection && random[uas_tk](0,1) == 0) {
					currentWound.infection = max(currentWound.infection - 1, 1);
				}

				HDPlayerPawn(owner).A_MuzzleClimb(
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1))
				);
			}
		}
	}
}
