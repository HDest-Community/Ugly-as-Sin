extend class UaS_TraumaKit {
	void HandleStapler() {
		statusMessage = statusmessage.."\cjStapler\n";
		statusMessage = statusmessage.."\cuManual hand-stapler.\n";
		statusMessage = statusmessage.."Staples remaining: \cy"..weaponstatus[TKS_STAPLES].."\n\n";
		if (!wh.currentWound) { return; }
		if (wh.currentWound.open <= 0) { return; }
		if (weaponstatus[TKS_STAPLES] <=0) {
			currentmessage.text = "No staples left!\n";
			currentmessage.timeout = 2*35;
			return;
		}

		// Status precheck
		if (wh.currentWound.open <= 0) {
			//currentmessage.text = "The wound is closed.\n";
			//currentmessage.timeout = 10;
			return;
		}

		// Handle actions
		if (weaponstatus[TK_HOLD] > 0) { weaponstatus[TK_HOLD]--; }
		if ((owner.player.cmd.buttons & BT_ATTACK) && !(owner.player.oldbuttons & BT_ATTACK) && (weaponstatus[TK_HOLD] <= 0)){
			if (!HandleStrip()) { return; }
			owner.A_StartSound("medikit/staple",CHAN_WEAPON);
			weaponstatus[TKS_STAPLES]--;
			if (wh.currentWound.inpain(25)) {
				patient.damagemobj(owner,owner,1,"staples");
				weaponstatus[TK_HOLD] = 50;
				PainJolt();
			}
			else {
				//PlaySkinSound(SKINSOUND_GRUNT,"*grunt");
				weaponstatus[TK_HOLD] = 15;
			}
			patient.A_MuzzleClimb(
				(frandom[uas_tk](-1,1),frandom[uas_tk](-1,1)),
				(frandom[uas_tk](-1,1),frandom[uas_tk](-1,1)),
				(frandom[uas_tk](-1,1),frandom[uas_tk](-1,1)),
				(frandom[uas_tk](-1,1),frandom[uas_tk](-1,1))
			);
			wh.currentWound.Patch(frandom[uas_tk](0.5,2), true);
		}
	}
}
