extend class UaS_Scarcity_Handler {
	static const name armourClassNames[] = {
		'GarrisonArmour',
		'BattleArmour',
		'HDCorporateArmour',
		'HDArmorPlate',
		'HDLeatherArmour',
		'HDHEVArmour',
		'HHelmet',
		'WAN_SneakingSuit'
	};

	bool DoOther(actor mo) {
		bool dbgActuallyHandled = false;

		Array< Class<Inventory> > armourClasses;

		armourClasses.clear();
		forEach(className : armourClassNames) {
			let cls = (Class<Inventory>)(className);

			if (cls) armourClasses.push(cls);
		}
		
		bool boxClass = (
			mo is 'HD9mBoxPickup'
			|| mo is 'HD7mBoxPickup'
			|| mo is 'ShellBoxPickup'
			|| mo is 'HD355BoxPickup'
		);
		bool shellClass = (
			mo is 'ShellPickup'
			|| mo is 'HDShellAmmo'
		);

		bool armourCls = false;
		forEach(armourClass : armourClasses) armourCls |= mo is armourClass;

		// Replacer routine for Armour-type items
		if (mo is 'HDArmour' || armourCls) {
			dbgActuallyHandled = true;
			HDMagAmmo a = HDMagAmmo(mo);
			name armourname = a.GetClassName();
			
			let min = (mo is 'HDArmour' && HDArmour(mo).mega) ? 1000 : 0;
			let max = a.mags[0];

			ArmourReplacer(armourname, min, max, mo);
			mo.destroy();
		}

		// Replacer routines for mag-type items
		else if (mo is 'HDMagAmmo') {
			dbgActuallyHandled = true;
			HDMagAmmo m = HDMagAmmo(mo);
			name magname = m.GetClassName();
			name roundtype = "";
			int mpu = m.maxperunit;
			// if (m is "HD4mMag") { mpu = 50; } // Maybe?
			if (m is "HD7mMag") { mpu = 30; }
			if (m is "HD7mClip") { mpu = 10; }
			// TODO: Add more support for addon mags that use recast system (PB's CAWS Mags)
			if (m.GetClassName() == "HD_CAWSMag") { mpu = 10; }
			if (m.GetClassName() == "HD_FALMag") { mpu = 75; }
			if (m.roundtype != "None") { roundtype =  m.roundtype.GetClassName(); }
			MagReplacer(magname, roundtype, min(m.mags[m.mags.size() - 1], mpu), mo);
			mo.Destroy();
		}
	
		// Replacer routines for HDUPK-derived and ammo items
		else if (boxClass) {
			dbgActuallyHandled = true;
			HDUPK b = HDUPK(mo);
			BoxReplacer(b.GetClassName(), b.pickuptype.GetClassName(), b.amount, mo);
			mo.Destroy();
		}
		else if (shellClass) {
			dbgActuallyHandled = true;
			UaS.SpawnStuff('HDFumblingShell', random(0, int(4 * UaS_ScarcityOverspawn)), mo.pos, true);
			mo.Destroy();
		}
		else if (mo is 'RocketBigPickup') {
			dbgActuallyHandled = true;
			for(int i = 1; i <= 5; i++) {
				if(UaS.RandomChance(UaS_ScarcityOverspawn)) {
					if(UaS.RandomChance(0.5)) { UaS.SpawnStuff('HDRocketAmmo', random(0, 1), mo.pos); }
					else { UaS.SpawnStuff('DudRocket', random(0, 1), mo.pos); }
				}
			}
			UaS.SpawnStuff('UaS_EmptyRocketBox', 1, mo.pos);
			mo.Destroy();
		}
		else if (mo is 'FragP' || mo is 'HDFragGrenadeAmmo') {
			dbgActuallyHandled = true;
			if(UaS.RandomChance(UaS_ScarcityOverspawn)) { UaS.SpawnStuff('FragP', random(0, 1), mo.pos); }
			else { UaS.SpawnStuff('HDFragSpoon', random(0, 1), mo.pos); }
			mo.Destroy();
		}

		// Replacer routines for equipment
		else if (mo is 'HDMap') {
			dbgActuallyHandled = true;
			let spawnedMap = UaS_Automap(actor.Spawn('UaS_Automap', mo.pos));
			if(spawnedMap) { spawnedMap.status = random(0, 2); }
			mo.Destroy();
		}
		else if (mo is 'PortableLiteAmp') {
			dbgActuallyHandled = true;
			let spawnedLiteAmp = PortableLiteAmp(actor.Spawn('PortableLiteAmp', mo.pos));
			double avgint = random(200, 800);
			avgint += random(200, 800);
			avgint += random(200, 800);
			avgint /= 3;
			avgint *= UaS_ScarcityOverspawn;
			avgint  = clamp(avgint,0,400);
			PortableLiteAmp(spawnedLiteAmp).setintegrity(int(avgint));
			UaS.LogDebug(DBG_Scarcity, string.format("Liteamp integrity %i", PortableLiteAmp(spawnedLiteAmp).getintegrity()));
			mo.Destroy();
		}

		if (dbgActuallyHandled) { dbgMsg = dbgMsg.."\n\t• otherwise modified or removed "..mo.getclassname(); }
		else { dbgMsg = dbgMsg.."\n\t• ignored unhandled class "..mo.getclassname(); }
		
		return true;
	}
}
