class UaS_Ally_Spawner : EventHandler {
	double spawnChance;

	override void WorldLoaded(WorldEvent e) {
		spawnChance = UaS_AllySpawnRate;
		spawnChance /= 100.0;
		UaS.LogDebug(DBG_Allies, string.format("Ally spawn debugging on. Spawnchance = "..spawnChance));
	}

	//Spawn allied marines randomly
	override void WorldThingSpawned(WorldEvent e) {
		bool replaced = false;

		// Conditions for skipping spawns
		if(!UaS.RandomChance(spawnChance)) { return; }
		if(level.maptime > 5) { return; }
		if(!e.Thing) { return; }

		bool corpseType = (
			e.Thing.GetClassName() == 'DeadZombieStormtrooper' ||
			e.Thing.GetClassName() == 'DeadZombieShotgunner' ||
			e.Thing.GetClassName() == 'DeadRifleman' ||
			e.Thing.GetClassName() == 'ReallyDeadRifleman'
		);

		if(corpseType) {
			//Make CheckPosition ignore the corpse
			bool prevNoclip = e.Thing.bNOCLIP;
			e.Thing.bNOCLIP = true;

			replaced = SpawnAlly(e.Thing.pos);
			if (replaced) {
				UaS.LogDebug(DBG_Allies, string.format("Spawned ally"));
				e.Thing.Destroy();
			}
			else { e.Thing.bNOCLIP = prevNoclip; }
		}
	}

	bool SpawnAlly(vector3 pos) {
		HDOperator ally = HDOperator(actor.spawn('HDOperator', pos));
		if (!ally) { return false; }
		if (!ally.CheckPosition(pos.xy)) {
			ally.Destroy();
			return false;
		}
		switch (UaS_AllyPreferred) {
			case 1:
				ally.painsound="marine/pain";
				ally.deathsound="marine/death";
				break;
			case 2:
				ally.painsound="marinef/pain";
				ally.deathsound="marinef/death";
				break;
		}
		ally.GiveInventory("UaS_Ally_Activator", 1);
		return true;
	}
}
