const UAS_HDLD_STABILZER = "stb";
const UAS_STB_SLOT = 25;

class UaS_GyroStabilizer : HDPickup {
	array<string> weaponList;
	array<int> AttachList;
	string statusText;
	int statusTimeout;
	double stabilize;

	Default {
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "Picked up a gyro stabilizer.";
		Inventory.Icon "UGSIR0";
		HDPickup.Bulk 10;
		Scale 0.75;
		tag "Gyroscopic Stabilizer";
		hdpickup.refid UAS_HDLD_STABILZER;
	}

	override void DoEffect() {
		super.DoEffect();
		statusTimeout = max(statusTimeout-1, 0);
		DoStabilize();
	}

	override void BeginPlay() {
		weaponList.push("hdpistol");
		weaponList.push("hdrevolver");
		weaponList.push("hdsmg");
		weaponList.push("hunter");
		weaponList.push("slayer");
		weaponList.push("zm66assaultrifle");
		weaponList.push("zm66semi");
		weaponList.push("zm66regular");
		weaponList.push("vulcanette");
		weaponList.push("liberatorrifle");
		weaponList.push("liberatornogl");
		weaponList.push("bossrifle");
		super.BeginPlay();
	}

	void SetStatusText(string text, int timeout = 35, bool force = false) {
		if (!force && statusTimeout > 0) { return; }
		statusText = text;
		statusTimeout = timeout;
	}

	void DoStabilize() {
		let ownr = HDPlayerPawn(owner);
		if(!ownr) { return; }
		if(!ownr.player.ReadyWeapon) { return; } // Skip if no ready weapon
		HDWeapon wep = HDWeapon(owner.player.ReadyWeapon); // Cast the readyweapon
		if (CheckAttached(wep) >= 0) {
			double minfactor = 0.15;
			if(ownr.beatmax<10||
				ownr.fatigue>20||
				ownr.bloodpressure>20||
				ownr.health<33){ minfactor = 0.50; }
			stabilize += GetCorrections();
			stabilize *= 0.9;
			double maxfactor = max(stabilize/(stabilize+20),minfactor);
			MultiplyRecoil(maxfactor);
			// console.printf(""..maxfactor);
			if (maxfactor >= 0.75) { SetStatusText("\crCALIBRATING ".."\cu"..frandom(maxfactor,maxfactor*1.1), 1); }
			else if (maxfactor >= 0.5) { SetStatusText("\cqSTABILIZING ".."\cu"..frandom(maxfactor,maxfactor*1.1), 1); }
			else { SetStatusText("\cdSTABILIZED ".."\cu"..frandom(maxfactor,maxfactor*1.1), 1); }
		}
	}

	bool Periodic(int p) { return (level.maptime % p == 0); }

	int GetCorrections() {
		let ownr = HDPlayerPawn(owner);
		if(!ownr) { return 0; }
		int cvalue = (
			ownr.muzzleclimb1.length()  +
			ownr.muzzleclimb2.length()  +
			ownr.muzzleclimb3.length()  +
			ownr.muzzleclimb4.length()  +
			ownr.wepbobrecoil1.length() +
			ownr.wepbobrecoil2.length() +
			ownr.wepbobrecoil3.length() +
			ownr.wepbobrecoil4.length() +
			ownr.muzzledrift.length() +
			ownr.vel.length());
		return cvalue;
		// return ceil(cvalue / max(cvalue-1,1));
	}

	void MultiplyRecoil(double factor) {
		let ownr = HDPlayerPawn(owner);
		if(!ownr) { return; }
		ownr.muzzleclimb1  *= factor;
		ownr.muzzleclimb2  *= factor;
		ownr.muzzleclimb3  *= factor;
		ownr.muzzleclimb4  *= factor;
		ownr.wepbobrecoil1 *= factor;
		ownr.wepbobrecoil2 *= factor;
		ownr.wepbobrecoil3 *= factor;
		ownr.wepbobrecoil4 *= factor;
		ownr.muzzledrift   *= factor;
		// if (ownr.scopecamera) {
			// ownr.scopecamera.angle *= factor;
			// ownr.scopecamera.pitch *= factor;
		// }
	}

	override void DrawHudStuff(hdstatusbar sb, hdplayerpawn hpl, int hdflags, int gzflags) {
		if (statusTimeout <= 0) { return; }
		sb.drawstring(sb.pnewsmallfont,
			statusText,
			(50,-50),
			flags:sb.DI_TEXT_ALIGN_LEFT|sb.DI_SCREEN_HCENTER|sb.DI_SCREEN_BOTTOM,
			translation:Font.CR_GRAY,scale:(0.5,0.5)
		);
	}

	override bool Use(bool pickup) {
		if (owner.player.cmd.buttons&BT_USE) {
			SetStatusText("\czUnpaired all UIDS", 35, true);
			AttachList.clear();
		}
		else {
			if(!owner.player.ReadyWeapon) { return false; } // Skip if no ready weapon
			HDWeapon wep = HDWeapon(owner.player.ReadyWeapon); // Cast the readyweapon
			AttachOrDetatch(wep);
		}
		return false;
	}

	void AttachOrDetatch(HDWeapon atdt) {
		int index = CheckAttached(atdt);
		if (index == -1) { AttachToWeapon(atdt); }
		else { DetachFromWeapon(atdt, index); }
	}

	// -1 means not in attach list
	int CheckAttached(HDWeapon cw) {
		int ret = -1;
		for (int i = 0; i < AttachList.size(); i++) {
			if (cw.weaponstatus[UAS_STB_SLOT] == AttachList[i]) {
				ret = i;
				break;
			}
		}
		return ret;
	}

	void DetachFromWeapon(HDWeapon dt, int index = -1) {
		if (index >= 0) {
			int UID = dt.weaponstatus[UAS_STB_SLOT];
			dt.weaponstatus[UAS_STB_SLOT] = 0;
			AttachList.delete(index);
			// console.printf("Detatched index "..index);
			A_StartSound("misc/fragknock", CHAN_BODY, volume: 0.3);
			SetStatusText("\czDetached \ccUID "..UID, 35, true);
		}
	}

	void AttachToWeapon(HDWeapon at) {
		if (AttachList.Size() >= 3) {
			SetStatusText("\cgMax pairings exceeded", 35, true);
			return;
		}
		bool validWeapon = false;
		string weapname = at.GetClassname();
		for(int i = 0; i < weaponList.size(); i++) {
			if(weapName == weaponList[i]) { validWeapon = true; }
		}
		if(!(validWeapon || at.weaponspecial == 1337)) { return; }
		int UID = random(int.max/4, int.max);
		at.weaponstatus[UAS_STB_SLOT] = UID;
		AttachList.push(UID);
		A_StartSound("misc/fragknock", CHAN_BODY, volume: 0.3);
		A_StartSound("misc/casing", CHAN_WEAPON, volume: 0.3);
		SetStatusText(string.format("\czAttached %i/3 \ccUID %i", AttachList.size(), UID), 70, true);
	}

	States {
		Spawn:
			UGSI R -1;
			stop;
	}
}
