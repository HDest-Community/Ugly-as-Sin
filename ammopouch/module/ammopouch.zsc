const UAS_HDLD_AMMOPOUCH = "apo";

//==================================================
// Base Definition
//==================================================
class UaS_AmmoPouch : HDBackpack {
	default {
		tag "Ammo Pouch";
		HDBackpack.MaxCapacity 200;
		Inventory.Icon "UGSPA0";
		Inventory.PickupMessage "Picked up an ammo pouch!";
		scale 0.6;
		hdweapon.wornlayer 0;
		hdweapon.refid UAS_HDLD_AMMOPOUCH;
	}

	override string, double GetPickupSprite() { return "UGSPA0", 1.0; }

	override void BeginPlay() {
		super.BeginPlay();
		Storage = new('AmmoPouch_ItemStorage');
		weaponstatus[APS_AMOUNT] = max(weaponstatus[APS_AMOUNT], 1);
		UpdateCapacity();
	}

	override void PostBeginPlay() {
		super.PostBeginPlay();
		weaponstatus[APS_AMOUNT] = max(weaponstatus[APS_AMOUNT], 1);
		UpdateCapacity();
	}

	// Thanks Accensus for figuring out multiple pouches in loadout codes!
	override void LoadoutConfigure(string input) {
		int amt = input.ToInt();
		if (amt > 0)
		{
			weaponstatus[APS_AMOUNT] = amt;
			UpdateCapacity();
		}
		super.LoadoutConfigure(input);
	}

	States {
		Spawn:
			UGSP ABC -1 NoDelay
			{
				if (invoker.Storage.TotalBulk ~== 0)
				{
					frame = 1;
				}
				else if (target)
				{
					translation = target.translation;
					frame = 2;
				}
				invoker.bNO_AUTO_SWITCH = false;
			}
			Stop;
		User3:
			#### # 0 A_MagManager("");
			goto Ready;
	}

	enum AmmoPouchSlots {
		APS_AMOUNT
	}
}
