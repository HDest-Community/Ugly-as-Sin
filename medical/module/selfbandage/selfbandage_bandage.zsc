extend class UaS_SelfBandage {
	void HandleBandage() {
		statusMessage = "\c[Gray]Bandage\n";
		statusMessage = statusMessage.."\c[DarkGray]Useful if you're bleeding out.";

		toolHelpText =
			WEPHELP_FIRE.."  Bandage wound\n"
			..WEPHELP_ALTFIRE.."  Rip bandage\n";

		if (owner.player.cmd.buttons & BT_FIREMODE) { return; }
		if (owner.player.cmd.buttons & BT_ATTACK) { TryBandage(); }
		else if (owner.player.cmd.buttons & BT_ALTATTACK) { TryRipBandage(); }
	}

	void BandageWound(float toPatch, float pressure) {
		if (!currentWound) { return; }

		currentWound.Patch(toPatch);
		if (currentWound.depth <= 0. || currentWound.pressure <= 0.3) {
			currentWound.pressure += pressure;
		}
	}

	void BandageEffect(int min, int max) {
		timer = Random(min, max);
		HDPlayerPawn(owner).A_MuzzleClimb((FRandom(-1.5, 1.8), FRandom(-2.4, 2.4)), (0, 0), (0, 0), (0, 0));
		patient.A_StartSound("bandage/rustle", CHAN_BODY, CHANF_OVERLAP);
	}

	void TryBandage() {
		if (CheckCovered() || !currentWound) { return; }
		if (patient == owner && owner.pitch < 45) { owner.pitch += 6; return; }
		if (currentWound.depth <= 0. && currentWound.patched <= 0.) {
			currentMessage.text = "Nothing to be done here.";
			currentMessage.timeout = 2 * 35;
			return;
		}
		else if (patient.CountInv("IsMoving") >= 4) {
			currentMessage.text =
				"You need "..((patient == owner)? "" : "them ")
				.."to stay still to bandage "..((patient == owner)? "yourself!" : "them!");
			currentMessage.timeout = 2 * 35;
			return;
		}

		currentMessage.text = "Attempting to bandage wound...";
		currentMessage.timeout = 10;
		if ((timer <= 0 && timeout <= 0) || actionType != ACTION_BANDAGE) {
			timeout = 5;
			progress = 0;
			actionType = ACTION_BANDAGE;
			owner.A_StartSound("bandage/rip", CHAN_WEAPON, CHANF_OVERLAP, 0.4);
			BandageEffect(5, 10);
			return;
		}
		else if (timer <= 0 && timeout > 0) { ++progress; }
		else { timeout = 5; return; }

		switch (progress) {
			case 1:
				BandageEffect(30, 40);
				break;

			case 2:
				BandageEffect(30, 35);
				break;

			case 3:
				BandageEffect(15, 20);
				break;

			default:
				BandageEffect(4, 8);
				patient.A_StartSound("weapons/pocket", CHAN_BODY, CHANF_OVERLAP);
				patient.A_ChangeVelocity(FRandom(-0.3, 0.3), FRandom(-0.3, 0.3), FRandom(-1, 2));

				progress = 0;
				BandageWound(FRandom(5, 15), FRandom(0.25, 0.95));
				break;
		}
	}

	void TryRipBandage() {
		if (CheckCovered() || !currentWound) { return; }
		if (patient == owner && owner.pitch < 45) { owner.pitch += 6; return; }
		if (currentWound.patched <= 0.) {
			currentMessage.text = "Nothing to be done here.";
			currentMessage.timeout = 2 * 35;
			return;
		}
		else if (patient.CountInv("IsMoving") >= 4) {
			currentMessage.text =
				"You need "..((patient == owner)? "" : "them ")
				.."to stay still to rip off "..((patient == owner)? "your " : "their ").."bandage!";
			currentMessage.timeout = 2 * 35;
			return;
		}

		currentMessage.text = "Ripping off bandage...";
		currentMessage.timeout = 10;
		if ((timer <= 0 && timeout <= 0) || actionType != ACTION_RIPBANDAGE) {
			timeout = 5;
			progress = 0;
			actionType = ACTION_RIPBANDAGE;
			BandageEffect(5, 10);
			return;
		}
		else if (timer <= 0 && timeout > 0) { ++progress; }
		else { timeout = 5; return; }

		switch (progress) {
			case 1:
			case 2:
			case 3:
			case 4:
				BandageEffect(4, 10);
				break;

			default:
				BandageEffect(0, 0);
				patient.A_StartSound("bandage/rip", CHAN_BODY, CHANF_OVERLAP);
				owner.A_ChangeVelocity(FRandom(-0.3, 0.3), FRandom(-0.3, 0.3), FRandom(-1, 2));

				progress = 0;
				currentWound.depth += currentWound.patched;
				currentWound.patched = 0;
				currentWound.pressure = 0;
				break;
		}
	}
}
