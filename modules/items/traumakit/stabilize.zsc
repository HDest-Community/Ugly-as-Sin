class UaS_WoundStabilization : HDWeapon {
	int pressure, hemostatic;
	string statusMessage;
	StatusInfo currentMessage;
	//WoundInfo currentWound;
	HDPlayerPawn patient;

	Default {
		Inventory.Amount 1;
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "";
		-INVENTORY.INVBAR;
		+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.IGNORESKILL;
		+WEAPON.NO_AUTO_SWITCH;
		//+WEAPON.NOAUTOFIRE;
		weapon.slotpriority 0.6;
		weapon.slotnumber 9;
		tag "Wound Stabilization";
	}
	override double weaponbulk(){ return 0; }

	override void DoEffect() {
		if (!(owner.player.readyweapon is 'UaS_WoundStabilization')) { return; }
		SetHelpText();

		statusMessage = "--- \cdWound stabilization\c- ---\n";
		if (patient) { statusMessage = statusmessage.."Treating \cg"..patient.player.getusername().."\n\n"; }
		else { statusMessage = statusmessage.."\n\n"; }

		statusMessage = statusmessage..
			"\cuApply pressure and\nhemostatic compound\nto bandaged wounds.\n\n\n";

		BleedingControl();
		TickMessages();
		A_WeaponMessage(statusMessage);
	}

	void BleedingControl() {
		SetPatient();
		if (patient.unstablewoundcount <= 0) {
			currentmessage.text = "";
			statusMessage = statusmessage.."No unstable bandaged wounds to treat.";
			hemostatic = 0;
			pressure = 0;
			return;
		}

		// Hemostatic amount readout
		if (hemostatic > 0) { statusMessage = statusmessage.."\ca"; }
		for (int i = 0; i < hemostatic; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."Hemostatic";
		for (int i = 0; i < hemostatic; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."\n";

		// Pressure amount readout
		if (pressure > 50) { statusMessage = statusmessage.."\cw"; }
		if (pressure > 100) { statusMessage = statusmessage.."\cm"; }
		for (int i = 0; i < pressure/10; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."Pressure";
		for (int i = 0; i < pressure/10; i++) { statusmessage = statusmessage.."="; }
		statusmessage = statusmessage.."\n\n";

		// Apply hemostatic compound
		if ((owner.player.cmd.buttons & BT_RELOAD)) {
			pressure = 0;
			if (hemostatic >= 5) {
				currentmessage.text = "Applied hemostatic compound.\n"
					.."\cr--WARNING--\n"
					.."Clotting agents can cause\n"
					.."burns and tissue damage!\n";
				currentmessage.timeout = 35;
				return;
			}
			else {
				if (!(owner.player.oldbuttons & BT_RELOAD)) { owner.A_StartSound("bandage/rip", CHAN_WEAPON, CHANF_DEFAULT, 0.5); }
				if (level.time % 20 == 0) {
					owner.A_StartSound("misc/wallchunks",CHAN_WEAPON);
					HDPlayerPawn(owner).A_MuzzleClimb(
						(frandom[uas_bc](-1,1),frandom[uas_bc](0,1))/2,
						(frandom[uas_bc](-1,1),frandom[uas_bc](0,1))/2,
						(frandom[uas_bc](-1,1),frandom[uas_bc](0,1))/2,
						(frandom[uas_bc](-1,1),frandom[uas_bc](0,1))/2
					);
					hemostatic++;
					currentmessage.text = "Applying hemostatic compound to wound.\n"
						.."\cr--WARNING--\n"
						.."Clotting agents can cause\n"
						.."burns and tissue damage!\n";
					currentmessage.timeout = 35;
				}
				return;
			}
		}

		// Apply pressure
		double shake;
		if ((owner.player.cmd.buttons & BT_ATTACK)) {
			pressure = min(pressure + 1, 99 + (hemostatic * 10));
			shake = pressure / 20.0;
			if (random[uas_bc](0,25) == 0) { patient.A_StartSound("misc/smallslop", CHAN_BODY); }
			if (pressure == 100) {
				patient.damagemobj(owner,owner,1,"staples");
				patient.burncount += randompick[uas_bc](0, 0, 0, 1);
			}
			if (pressure == 109) {
				pressure = 0;
				patient.damagemobj(owner,owner,1,"staples");
				patient.burncount += randompick[uas_bc](0, 1);
			}
		}
		else {
			pressure = max(pressure - 1, 0);
			shake = -pressure / 20.0;
		}

		// Shake and fluctuate view
		HDPlayerPawn(owner).A_MuzzleClimb(
			(frandom[uas_bc](-shake,shake),frandom[uas_bc](0,shake))/20,
			(frandom[uas_bc](-shake,shake),frandom[uas_bc](0,shake))/30,
			(frandom[uas_bc](-shake,shake),frandom[uas_bc](0,shake))/40,
			(frandom[uas_bc](-shake,shake),frandom[uas_bc](0,shake))/50
		);

		// check for stopped bleeding
		bool canstabilize = (
			(hemostatic <= 0) ||
			(hemostatic > 0 && pressure <= 100)
		);
		if (level.time % 10 == 0 && canstabilize) {
			int upperlimit = pressure + (hemostatic * 10);
			int check = (
				random[uas_bc](0, upperlimit) +
				random[uas_bc](0, upperlimit)) / 2;

			if (check > 70) {
				currentmessage.text = "Stabilized some bleeding.";
				currentmessage.timeout = 35;
				hemostatic = max(hemostatic - 1, 0);
				pressure = 50;
				patient.unstablewoundcount = max(patient.unstablewoundcount - 1, 0);
				patient.oldwoundcount = max(patient.oldwoundcount + 1, 0);
			}
		}
	}

	void SetPatient() {
		HDPlayerPawn other;
		FLineTraceData tktrace;
		owner.LineTrace(owner.angle, 42, owner.pitch, offsetz: owner.height-12, data: tktrace);
		if (tktrace.hitactor && tktrace.hitactor is "HDPlayerPawn") { other = HDPlayerPawn(tktrace.hitactor); }

		if (other && (owner.player.cmd.buttons & BT_ZOOM)) { patient = other; }
		if (!patient || !other) { patient = HDPlayerPawn(owner); }
	}

	struct StatusInfo {
		string text;
		int timeout;
	}

	override string gethelptext(){
		string r;
		r.AppendFormat(WEPHELP_FIRE.."  Hold to apply pressure\n");
		r.AppendFormat(WEPHELP_RELOAD.."  Hold to apply hemostatics\n");
		r.AppendFormat(WEPHELP_ZOOM.."  Treat another player\n");
		return r;
	}

	void SetHelpText(){ //non-action version
		let hdp=hdplayerpawn(owner);if(hdp){
			string ttt=self.gethelptext();
			if(ttt!="")hdp.wephelptext="\cu"..gettag().."\n"..ttt;
			else hdp.wephelptext=ttt;
		}
	}

	void TickMessages() {
		// Tick down transient messages
		if (currentmessage.timeout > 0) {
			statusmessage = statusmessage..currentmessage.text;
			currentmessage.timeout--;
		}
	}

	// Copied from HD, non-action version
	void PlaySkinSound(int which,sound fallback,int channel=CHAN_VOICE,int flags=0){
		let ppp=hdplayerpawn(owner);
		sound sss;
		if(ppp)switch(which){
		case SKINSOUND_TAUNT:sss=ppp.tauntsound;break;
		case SKINSOUND_XDEATH:sss=ppp.xdeathsound;break;
		case SKINSOUND_GRUNT:sss=ppp.gruntsound;break;
		case SKINSOUND_LAND:sss=ppp.landsound;break;
		case SKINSOUND_MEDS:sss=ppp.medsound;break;
		default:return;
		}
		else sss=fallback;
		owner.A_StartSound(sss,channel,flags);
	}
}
