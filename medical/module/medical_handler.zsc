class UaS_WoundHandler : Inventory {
	array<WoundInfo> critwounds;
	int nextwound;

	default {
		+INVENTORY.PERSISTENTPOWER;
		+INVENTORY.UNDROPPABLE;
		-INVENTORY.INVBAR;
		+INVENTORY.UNTOSSABLE;
	}

	override void DoEffect() {
		if (!owner) { return; }
		ManageWounds();
		WoundPainShake();
	}

	void WoundPainShake() {
		if(critwounds.size() <= 0) { return; }
		double maxpain;
		for(int i = 0; i < critwounds.size(); i++) {
			maxpain = max(maxpain, critwounds[i].pain);
		}
		double ps = min(maxpain / 200, 1.5);
		HDPlayerPawn(owner).A_MuzzleClimb(
			ps*(frandom[uas_wh](-1,1),ps*frandom[uas_wh](-1,1)),
			ps*(frandom[uas_wh](-1,1),ps*frandom[uas_wh](-1,1)),
			ps*(frandom[uas_wh](-1,1),ps*frandom[uas_wh](-1,1)),
			ps*(frandom[uas_wh](-1,1),ps*frandom[uas_wh](-1,1))
		);
	}

	void ManageWounds() {
		if (nextwound == 0) { nextwound = random[uas_wh](1, 5); }
		HDPlayerPawn plr = HDPlayerPawn(owner);
		if(!plr) return;

		// iterate wound array, drop any wounds marked "clear"
		// Do this first to hopefully avoid dropping or double-counting
		for(int i = 0; i <= critwounds.size() - 1; i++) {
			if (!critwounds[i]) { critwounds.delete(i); continue; }
			if (critwounds[i].clear) {
				UaS.LogDebug(DBG_Med, string.format("Cured "..critwounds[i].description), plr);
				plr.A_Log("Your condition improves.", true);
				plr.oldwoundcount = max(plr.oldwoundcount - critwounds[i].size, 0);
				RemoveWound(i);
				continue;
			}
			critwounds[i].TickWound();
			critwounds[i].painkiller = max(critwounds[i].painkiller, plr.countinv("HDStim") * (50./float(HDStim.HDSTIM_DOSE)));
		}

		// tabulate new wound array total points
		int knownwounds;
		for (int i = 1; i < critwounds.size(); i++) {
			knownwounds += critwounds[i].size;
		}

		//compare
		int diffwounds = plr.oldwoundcount - knownwounds;

		// Create a new wound entry until the wounds array matches number of oldwounds
		if (diffwounds >= nextwound) {
			int newsize = nextwound;
			newsize = min(newsize, diffwounds);
			critwounds.push(WoundInfo.Create(newsize));
			UaS.LogDebug(DBG_Med, string.format("created "..critwounds[critwounds.size() - 1].description.." with size "..newsize));
			nextwound = random[uas_wh](1, 5);
		}

		// remove oldest wound entry if passively healed enough points or if wounds are zero
		if (critwounds.size() > 0 && (diffwounds < 0 || plr.oldwoundcount <= 0)) {
			bool removedone;
			// Look for oldest wound of the right size to remove
			for (int i = 0; i < critwounds.size(); i++) {
				if (critwounds[i].size == abs(diffwounds)) {
					UaS.LogDebug(DBG_Med, string.format("passively healed "..critwounds[i].description.." with size "..critwounds[i].size));
					RemoveWound(i);
					removedone = true;
					break;
				}

			}
			//Remove the oldest wound if none appropriate
			if (!removedone && (diffwounds < -5 || plr.oldwoundcount <=0)) {
				UaS.LogDebug(DBG_Med, string.format("passively healed "..critwounds[0].description.." with size "..critwounds[0].size.. "for diffwound "..diffwounds));
				RemoveWound(0);
			}
		}
	}

	void RemoveWound(int w) {
		critwounds[w].destroy();
		critwounds.delete(w);
	}
}
