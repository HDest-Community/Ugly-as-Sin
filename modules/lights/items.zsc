class UaS_Glowstick : HDPickup {
	bool Activated;
	DynamicLight GlowLight;
	UaS_Glowstick DroppedGlowStick;

	Default {
		Inventory.Amount 1;
		Inventory.PickupMessage "Picked up a glowstick.";
		Inventory.Icon "UGSIB0";
		HDPickup.Bulk 2;
		HDPickup.NiceName "Glowstick";
		HDPickup.refid UAS_HDLD_GLOWSTICK;
		+FLATSPRITE;
		+FORCEXYBILLBOARD;
	}

	override void Tick() {
		switch(Activated) {
			case true:
				SetStateLabel("LightOn");
				if (!GlowLight) {
					GlowLight = DynamicLight(Actor.Spawn("DynamicLight"));
					GlowLight.args[DynamicLight.LIGHT_RED] = 32;
					GlowLight.args[DynamicLight.LIGHT_GREEN] = 128;
					GlowLight.args[DynamicLight.LIGHT_BLUE] = 32;
					GlowLight.args[DynamicLight.LIGHT_INTENSITY] = 64;
				}
				if (owner) { GlowLight.target = owner; }
				else { GlowLight.target = self; }
				break;
			case false:
				SetStateLabel("LightOff");
				if (GlowLight) { glowLight.Destroy(); }
				break;
		}
		super.Tick();
	}

	override bool Use(bool pickup) {
		if (!Activated) {
			owner.A_PlaySound("UaS/Glowstick", 0.15);
			Activated = true;;
		}
		return false;
	}

	override inventory CreateTossable(int amt) {
		DroppedGlowStick = null;
		DroppedGlowStick = UaS_Glowstick(super.CreateTossable(amt));
		if (DroppedGlowStick) {
			DroppedGlowStick.Activated = Activated;
			if(owner) { Activated = false; }
		}
		return DroppedGlowStick;
	}

	override void ActualPickup(Actor toucher) {
		if (Activated) { return; }
		super.ActualPickup(toucher);
	}

	States {
		Spawn:
			UGSI B -1;
			stop;
	}
	states {
		Spawn:
		LightOff:
			UGSI B 35;
			loop;
		LightOn:
			UGSI C 35 BRIGHT;
			loop;
	}

}
