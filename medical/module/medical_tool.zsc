// Any medical tool that requires a fancy interface for whatever reason
class UaS_MedicalTool : HDWeapon {
	StatusInfo currentMessage; // used for UI messages

	UaS_MedicalWheel wheel;
	UaS_MedicalToolInfo mti;

	Default {
		+Weapon.WIMPY_WEAPON;
		+Weapon.NO_AUTO_SWITCH;
		Weapon.SlotNumber 9;
	}

	override void DoEffect() {
		if (!wheel || wheel.owner != owner) { wheel = UaS_MedicalWheel(owner.FindInventory("UaS_MedicalWheel")); }
		if (!mti || mti.owner != owner) { mti = UaS_MedicalToolInfo(owner.FindInventory("UaS_MedicalToolInfo")); }
		if (mti) { mti.SetPatient(); }
	}

	UaS_Wound GetWound(int index) {
		if (mti.wh.critwounds.size() <= 0) { return null; }
		return mti.wh.critwounds[index];
	}

	ui string GetStatusColor(int amount) {
		if (amount >= 90) { return "\cm"; } // Black
		if (amount >= 80) { return "\cr"; } // Dark Red
		if (amount >= 70) { return "\ca"; } // Brick
		if (amount >= 60) { return "\cx"; } // Fire
		if (amount >= 50) { return "\ci"; } // Orange
		if (amount >= 40) { return "\ck"; } // Yellow
		if (amount >= 30) { return "\cs"; } // Dark Brown
		if (amount >= 20) { return "\cq"; } // Dark Green
		if (amount >= 10) { return "\cd"; } // Green
		if (amount >= 0) { return "\cd"; } // Grey (wait, isn't this green?)
		return "\cj"; // White
	}

	ui string GetOpenStatus(UaS_Wound currentWound) {
		string statusText = GetStatusColor(currentWound.open / 15 * 10).."open\c-";
		if (currentWound.depth <= 0. && currentWound.patched > 0.) { statusText = statusText.." but \c[Yellow]patched"; }
		else if (currentWound.patched > 0.) { statusText = statusText.." but \c[Orange]somewhat patched"; }
		else if (currentWound.depth <= 0.) { statusText = "\c[Green]sealed"; }

		return statusText;
	}

	struct StatusInfo {
		string text;
		int timeout;
	}
}

class UaS_MedicalToolInfo : Inventory {
	UaS_WoundHandler wh;
	HDPlayerPawn patient, lastpatient;
	UaS_Wound currentWound;
	int currentWoundIdx;

	override void BeginPlay() {
		currentWoundIdx = 0;
		Super.BeginPlay();
	}

	override void DoEffect() {
		// Make sure currentWoundIdx doesn't wrap around to 0 when updating
		if (wh && wh.critwounds.Size() > 0) { currentWoundIdx = min(currentWoundIdx, wh.critwounds.Size() - 1); }
		if (!currentWound) { UpdateCurrentWound(currentWoundIdx); }
	}

	void UpdateCurrentWound(int idx) {
		if (!wh || wh.critwounds.Size() <= 0) { currentWound = null; currentWoundIdx = 0; return; }

		currentWoundIdx = idx % wh.critwounds.Size();
		if (currentWoundIdx < 0) { currentWoundIdx = wh.critwounds.Size() - 1; }
		currentWound = wh.critwounds[currentWoundIdx];
	}

	void SetPatient() {
		HDPlayerPawn other;
		FLineTraceData tktrace;
		owner.LineTrace(owner.angle, 42, owner.pitch, offsetz: owner.height-12, data: tktrace);
		if (tktrace.hitactor && tktrace.hitactor is "HDPlayerPawn") { other = HDPlayerPawn(tktrace.hitactor); }

		if (other && (owner.player.cmd.buttons & BT_ZOOM)) { patient = other; }
		if (!patient || levellocals.Vec3Diff(owner.pos, patient.pos).length() > owner.radius * 4) { patient = HDPlayerPawn(owner); }

		wh = UaS_WoundHandler(patient.FindInventory('UaS_WoundHandler'));
		if (!wh) { console.printf("No wound handler!"); return; }
		if (patient != lastpatient) { UpdateCurrentWound(0); }
		lastpatient = patient;
	}
}
