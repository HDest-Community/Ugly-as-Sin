// AHHHHHHH, I NEED A MEDIC BAG
class UaS_SelfBandage : UaS_MedicalTool {
	int progress;
	int timer;
	int timeout;
	string statusMessage;
	BandageActions actionType;

	Default {
		Tag "$TAG_SELFBANDAGE";
		+Inventory.UNTOSSABLE;
		+HDWeapon.DONTDISARM;
	}

	override void PostBeginPlay() {
		Super.PostBeginPlay();
		weaponstatus[0] = TYPE_BANDAGE;
		timer = 0;
		progress = 0;
		actionType = ACTION_NONE;
	}

	override string GetHelpText() {
		if (!mti || !mti.UaS_Medical_SwitchZoom) { return ""; }
		string r = "";
		LocalizeHelp();
		string keySetPatient, keyAltFire;
		if (mti.UaS_Medical_SwitchZoom.GetBool()) { keySetPatient = LWPHELP_ALTFIRE; keyAltFire = LWPHELP_ZOOM; }
		else { keySetPatient = LWPHELP_ZOOM; keyAltFire = LWPHELP_ALTFIRE; }
		if (mti.UaS_Medical_SwitchFiremode.GetBool()) {
			r.AppendFormat(
				Stringtable.Localize("$HELPTEXT_SELFBDGALT"),
				LWPHELP_RELOAD,
				LWPHELP_ALTRELOAD,
				LWPHELP_FIREMODE,
				LWPHELP_RELOAD, LWPHELP_ALTRELOAD,
				keySetPatient
			);
		}
		else {
			r.AppendFormat(
				Stringtable.Localize("$HELPTEXT_SELFBDG"),
				LWPHELP_RELOAD, LWPHELP_ALTRELOAD,
				LWPHELP_FIREMODE,
				LWPHELP_RELOAD,
				LWPHELP_ALTRELOAD,
				keySetPatient
			);
		}

		switch (weaponstatus[0]) {
			default:
				r.AppendFormat(
					Stringtable.Localize("$HELPTEXT_SELFBDG_BANDAGE"),
					LWPHELP_FIRE,
					keyAltFire
				);
				break;
		}
		return r;
	}

	override Inventory CreateTossable() {
		HDWoundFixer.DropMeds(owner, 0);
		return null;
	}

	void ResetAction() {
		timer = 0;
		progress = 0;
		actionType = ACTION_NONE;
	}

	override void DoEffect() {
		if (timer >= 0) { --timer; }
		if (timeout >= 0) { --timeout; }
		if (timeout <= 0) { ResetAction(); }
		if (currentMessage.timeout > 0) { --currentMessage.timeout; }
		else { currentMessage.text = ""; }

		UaS_MedicalToolInfo.SetToolInfo(mti, HDPlayerPawn(owner));
		if (!(owner.player.ReadyWeapon is "UaS_SelfBandage")) { return; }

		mti.SetPatient();
		CycleWounds();
		TryRipBloodbag();
		TryStripWorn();

		switch (weaponstatus[0]) {
			default:
				HandleBandage();
				break;
		}

		// update helptext
		let hdp = HDPlayerPawn(owner);
		if (hdp) { hdp.wephelptext = "\cu"..GetTag().."\n"..GetHelpText(); }
	}

	bool CheckCovered() {
		let blockinv = HDWoundFixer.CheckCovered(mti.patient, CHECKCOV_ONLYFULL);
		if (blockinv) {
			string text = "$UI_SELFBDG_TAKEOFF_"..((mti.patient == owner)? "YOU" : "THEY");
			currentMessage.text = string.Format(Stringtable.Localize(text), blockinv.GetTag());
			currentMessage.timeout = 2 * 35;
			return true;
		}

		return false;
	}

	// also updates wounds
	void CycleWounds() {
		if (mti.wh.critwounds.Size() == 0 || !mti.PressedKey(mti.KEY_FIREMODE, false)) { return; }

		if (mti.PressedKey(mti.KEY_CYCLENEXT, true)) {
			mti.UpdateCurrentWound(mti.currentWoundIdx + 1);
			ResetAction();
			currentMessage.timeout = 0;
		}
		else if (mti.PressedKey(mti.KEY_CYCLEPREV, true)) {
			mti.UpdateCurrentWound(mti.currentWoundIdx - 1);
			ResetAction();
			currentMessage.timeout = 0;
		}
	}

	void TryRipBloodbag() {
		if (
			!(!mti.PressedKey(mti.KEY_FIREMODE, false) && owner.player.cmd.buttons & BT_ALTRELOAD)
			|| CheckCovered()
		) { return; }
		if (!mti.patient.CountInv("BloodBagWorn")) {
			string text = "$UI_SELFBDG_NOBLOODBAG_"..((mti.patient == owner)? "YOU" : "THEY");
			currentMessage.text = Stringtable.Localize(text);
			currentMessage.timeout = 2 * 35;
			return;
		}

		currentMessage.text = Stringtable.Localize("$UI_SELFBDG_RIPPINGOFFBAG");
		currentMessage.timeout = 10;
		if ((timer <= 0 && timeout <= 0) || actionType != ACTION_RIPBLOODBAG) {
			timeout = 5;
			progress = 0;
			actionType = ACTION_RIPBLOODBAG;
			BandageEffect(10, 15);
			owner.A_StartSound("bandage/pocket", CHAN_BODY, CHANF_OVERLAP);
			return;
		}
		else if (timer > 0 && timeout > 0) { timeout = 5; return; }

		int prevWoundCount = mti.wh.critwounds.Size();
		mti.patient.A_SetBlend("7a 3a 18", 0.1, 4);
		HDPlayerPawn(owner).A_MuzzleClimb((0, 2), (0, 0), (0, 0), (0, 0));
		HDPlayerPawn.PlaySkinSound(mti.patient, SKINSOUND_MEDS, "*usemeds");
		mti.patient.DropInventory(mti.patient.FindInventory("BloodBagWorn"));

		// patch up wound
		mti.wh.UpdateWounds();
		for (int i = min(prevWoundCount, mti.wh.critwounds.Size() - 1); i < mti.wh.critwounds.Size(); i++) {
			UaS_Wound wound = mti.wh.critwounds[i];
			if (wound.source != mti.patient) { continue; }

			wound.patched = wound.depth;
			wound.depth = 0;
			wound.pressure = frandom(1.0, wound.patched + 1.0);
		}
	}

	void TryStripWorn() {
		if (!(
			!mti.PressedKey(mti.KEY_FIREMODE, false)
			&& owner.player.cmd.buttons & BT_RELOAD
			&& !(owner.player.oldbuttons & BT_RELOAD)
		)) { return; }
		if (HDPlayerPawn.CheckStrip(mti.patient, mti.patient, silent: true)) {
			currentMessage.text = Stringtable.Localize("$UI_SELFBDG_NOTHINGTOSTRIP");
			currentMessage.timeout = 2 * 35;
			return;
		}
	}

	enum BandageTypes {
		TYPE_IMPROVISED,
		TYPE_BANDAGE,
	};

	enum BandageActions {
		ACTION_NONE,
		ACTION_BANDAGE,
		ACTION_RIPBANDAGE,
		ACTION_RIPBLOODBAG,
	};
}
