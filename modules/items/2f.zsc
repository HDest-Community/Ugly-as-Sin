// spare Second Flesh cartridges
class UaS_2F_Handler : Inventory {
	int cooldown;

	override void DoEffect() {
		super.DoEffect();
		//assume 7 auto-sutures per module, default 42 sutures = 6 modules
		let ownr = HDPlayerPawn(owner);
		if(!ownr || !ownr.player.ReadyWeapon) { return; }
		if(cooldown == 0 && ownr.player.ReadyWeapon.GetClassName() == 'HDMedikitter') {
			if(ownr.player.cmd.buttons & BT_USER4) {
				if(HDMedikitter(ownr.player.ReadyWeapon).weaponstatus[MEDS_SECONDFLESH] >= 7) {
					HDMedikitter(ownr.player.ReadyWeapon).weaponstatus[MEDS_SECONDFLESH] -= 7;
					owner.A_GiveInventory('UaS_2FCartridge', 1);
					owner.A_PlaySound("weapons/rifleclick", volume: 0.5);
					cooldown = 20;
				}
				else { HDMedikitter(ownr.player.ReadyWeapon).A_WeaponMessage("Can't remove partial auto-suture cartridge", 70); }
			}

			else if((ownr.player.cmd.buttons & BT_USER1) && owner.countinv('UaS_2FCartridge') > 0) {
				if(HDMedikitter(ownr.player.ReadyWeapon).weaponstatus[MEDS_SECONDFLESH] <= MEDIKIT_MAXFLESH - 7) {
					HDMedikitter(ownr.player.ReadyWeapon).weaponstatus[MEDS_SECONDFLESH] += 7;
					owner.A_TakeInventory('UaS_2FCartridge', 1);
					owner.A_PlaySound("weapons/rifleload", volume: 0.75);
					cooldown = 20;
				}
				else { HDMedikitter(ownr.player.ReadyWeapon).A_WeaponMessage("Not enough space to insert auto-suture cartridge", 70); }
			}
		}
		cooldown = clamp(cooldown - 1, 0, 20);
	}
}

class UaS_2FCartridge : HDPickup {
	Default {
		Inventory.Amount 1;
		inventory.pickupmessage "Picked up an auto-suture cartridge.";
		hdpickup.bulk 6;
		Inventory.Icon "HSCVP0";
		+INVENTORY.INVBAR;
		+INVENTORY.IGNORESKILL;
		+Inventory.KEEPDEPLETED;
		scale 0.2;
		tag "Auto-Suture Cartridge";
		hdpickup.refid UAS_HDLD_2FMOD;
	}

	action void HelpMessage() {
		if(getcvar("hd_helptext")) { console.printf("With your medikit open, press secondary reload to insert the cartridge, and unload to remove the cartridge."); }
	}

	States {
		Spawn:
			HSCV P -1;
			stop;
		Use:
			HSCV P 1 HelpMessage();
			fail;
	}
}
