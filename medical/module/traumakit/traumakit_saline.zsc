extend class UaS_TraumaKit {
	void HandleSaline() {
		statusMessage = statusmessage..Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_TITLE");
		statusMessage = statusmessage..Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_DESCRIPTION");
		statusMessage = statusmessage..Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_REMAINING")..weaponstatus[TKS_SALINE]..Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_NEWLINES");
		if (!mti.currentWound) { return; }
		if (mti.currentWound.open <= 0) { return; }
		if (weaponstatus[TKS_SALINE] <=0) {
			currentmessage.text = Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_NONELEFT");
			currentmessage.timeout = 2*35;
			return;
		}

		// Status precheck
		if (mti.currentWound.dirty < 0) {
			currentmessage.text = Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_UNNECESSARY");
			currentmessage.timeout = 10;
			return;
		}

		// Handle actions
		if (weaponstatus[TK_HOLD] < 0) {
			weaponstatus[TK_HOLD] = min(weaponstatus[TK_HOLD] + 1, 0);
			return;
		}

		if ((owner.player.cmd.buttons & BT_ATTACK)) {
			if (!HandleStrip()) { return; }
			if (!(owner.player.oldbuttons & BT_ATTACK)) {
				weaponstatus[TK_HOLD] = random[uas_tk](20,35);
				owner.A_StartSound("misc/smallslop", CHAN_WEAPON);
			}
			else {
				weaponstatus[TK_HOLD]--;
			}

			if (weaponstatus[TK_HOLD] > 0) {
				currentmessage.text = Stringtable.Localize("$UAS_TRAUMAKIT_SALINE_FLUSHING");
				currentmessage.timeout = 10;

				weaponstatus[TKS_SALINE]--;

				// semi-normalized distribution for success check
				int check = (
					random[uas_tk](0,75) +
					random[uas_tk](0,100)) / 2;

				if (mti.currentWound.inpain(5)) {
					weaponstatus[TK_HOLD] = -5;
					mti.currentWound.inpain(35);
					PainJolt();
				}

				// remove some dirt
				if (check < mti.currentWound.dirty) {
					owner.A_StartSound("misc/smallslop", CHAN_WEAPON);
					mti.currentWound.dirty = max(mti.currentWound.dirty - random[uas_tk](1,2), 0);
					if(random[uas_tk](0, 35) == 0) {
						mti.patient.A_StartSound(mti.patient.gruntsound, CHAN_VOICE);
					}
				}

				// maybe remove some infection
				if (check < mti.currentWound.infection && random[uas_tk](0,1) == 0) {
					mti.currentWound.infection = max(mti.currentWound.infection - 1, 1);
				}

				HDPlayerPawn(owner).A_MuzzleClimb(
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1)),
					(frandom[uas_tk](-.1,.1),frandom[uas_tk](.01,.1))
				);
			}
		}
	}
}
