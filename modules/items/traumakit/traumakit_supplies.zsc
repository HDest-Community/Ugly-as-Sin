extend class UaS_TraumaKit {
	override void actualpickup(actor other,bool silent){
		let tk=hdweapon(other.findinventory("UaS_TraumaKit"));
		if(tk) {
			other.A_Log(string.format("\cgRefilled Trauma Kit supplies"),true);
			tk.weaponstatus[TKS_PAINKILLER] += weaponstatus[TKS_PAINKILLER];
			tk.weaponstatus[TKS_SALINE] += weaponstatus[TKS_SALINE];
			tk.weaponstatus[TKS_BIOFOAM] += weaponstatus[TKS_BIOFOAM];
			tk.weaponstatus[TKS_STAPLES] += weaponstatus[TKS_STAPLES];
			self.destroy();
			return;
		}
		super.actualpickup(other,silent);
	}

	override void initializewepstats(bool idfa){
		super.initializewepstats();
		weaponstatus[TKS_PAINKILLER] = 20;
		weaponstatus[TKS_SALINE] = 1000;
		weaponstatus[TKS_BIOFOAM] = 1000;
		weaponstatus[TKS_STAPLES] = 100;
	}

	void HandleSupplies() {
		if((owner.player.cmd.buttons & BT_ALTFIRE) && !(owner.player.oldbuttons & BT_ALTFIRE)) {

			switch (weaponstatus[TK_SELECTED]) {
				case T_PAINKILLER:
					if(owner.countinv("PortableStimpack") > 0) {
						owner.TakeInventory("PortableStimpack", 1);
						weaponstatus[TKS_PAINKILLER] += 20;
						currentmessage.text = "\cvRefilled anesthetic.\nconsumed one stimpack";
						currentmessage.timeout = 2*35;
					}
					else {
						currentmessage.text = "No available stimpacks.";
						currentmessage.timeout = 2*35;
					}
					break;
				case T_SALINE:
					if(owner.countinv("SecondBlood") > 0) {
						owner.TakeInventory("SecondBlood", 1);
						weaponstatus[TKS_SALINE] += 1000;
						currentmessage.text = "\cvRefilled saline.\nconsumed one Second Blood pack";
						currentmessage.timeout = 2*35;
					}
					else {
						currentmessage.text = "No available Second Blood packs.";
						currentmessage.timeout = 2*35;
					}
					break;
				case T_FORCEPS:
					break;
				case T_BIOFOAM:
				case T_STAPLER:
					if(owner.countinv("UaS_2FCartridge") > 0) {
						owner.TakeInventory("UaS_2FCartridge", 1);
						weaponstatus[TKS_BIOFOAM] += 250;
						weaponstatus[TKS_STAPLES] += 25;
						currentmessage.text = "\cvRefilled staples and biofoam.\nconsumed one auto-suture cartridge";
						currentmessage.timeout = 2*35;
					}
					else {
						currentmessage.text = "No available auto-suture cartridges.";
						currentmessage.timeout = 2*35;
					}
					break;
				case T_SUTURES:
					break;
				case T_SCALPEL:
					break;
			}
		}
	}
}
