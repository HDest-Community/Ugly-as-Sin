class SlingInitialized : Inventory {}

class UaS_Sling_Bootstrap : EventHandler {
	override void PlayerEntered(PlayerEvent e) {
		PlayerInfo player = players[e.PlayerNumber];
		if(!player.mo) { return; } // The player might not have a PlayerPawn here
		Initialize(player);
	}

	//initialize additional player inventory
	void Initialize(PlayerInfo player) {
		if(!UaS_SlingEnabled) {
			player.mo.TakeInventory("SlingInitialized", 1);
			player.mo.TakeInventory("UaS_WeaponSling", 1);
			return;
		}
		if(player.mo.countinv("SlingInitialized")) { return; }
		if(UaS_SlingStart == true) {
			player.mo.Giveinventory("UaS_WeaponSling", 1);
		}
		player.mo.GiveInventory("SlingInitialized", 1);
	}

	override void WorldThingSpawned(WorldEvent e) {
		if(!UaS_SlingEnabled) { return; }
		if(level.maptime > 1) { return; }
		if(!e.Thing) { return; }
		if(e.Thing is "Inventory" && Inventory(e.Thing).Owner) { return; }

		bool spawnable = (
			e.Thing.GetClassName() == "hdsmg" ||
			e.Thing.GetClassName() == "hunter" ||
			e.Thing.GetClassName() == "slayer" ||
			e.Thing.GetClassName() == "zm66assaultrifle" ||
			e.Thing.GetClassName() == "zm66semi" ||
			e.Thing.GetClassName() == "zm66regular" ||
			e.Thing.GetClassName() == "vulcanette" ||
			e.Thing.GetClassName() == "liberatorrifle" ||
			e.Thing.GetClassName() == "liberatornogl" ||
			e.Thing.GetClassName() == "bossrifle"
		);

		if (spawnable && UaS.RandomChance(0.5)) { UaS.SpawnStuff("UaS_WeaponSling", 1, e.Thing.pos); }
	}
}
